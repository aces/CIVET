#!/usr/bin/env perl

# creates a grid from the classified image. This grid can then
# be used to initialise the solving of Laplace's equation.
#
# Authors: June-sic Kim <luck3d@bic.mni.mcgill.ca> and
#          Jason Lerch <jason@bic.mni.mcgill.ca>
# Updated by Oliver Lyttelton to fit the hemispheres
#
# Sep 2003
#
# Copyright Alan C. Evans
# Professor of Neurology
# McGill University
#

use strict;
use POSIX;
use MNI::Startup;
use Getopt::Tabular;
use MNI::Spawn;
use MNI::DataDir;
use MNI::FileUtilities qw(check_output_dirs);

# ===== Global Variables =====
my ($usage, $help);
my ($t1, $skull_mask, $skelCSF, $cls, $pve_gm, $pve_disc, $callosal_mask, 
    $wmSurfaceLeft, $wmSurfaceRight, 
    $grid, $laplace, $output);
my ($expression);
my ($minc_model);
my ($clsMasked, $wmLineLeft,$wmLineRight,$wmLine, $wmMask,$wmMaskLeft, $wmMaskRight, $filledImage, $rslCSF);
my ($in_chamfer, $out_chamfer);

# ===== Argument Processing =====

$usage = "$ProgramName [options] skeletonized_csf.mnc skull_mask.mnc wm_left_surface.obj wm_right_surface.obj classified.mnc pve_gm.mnc pve_disc.mnc callosal_mask.mnc output.mnc\n";
$help = "Help still to be written";

my @leftOverArgs;
my @argTbl = 
    (
     @DefaultArgs,
     ["-like", "string", 1, \$minc_model,
      "Reference file for resolution of a Laplacian field"],
     );
GetOptions(\@argTbl, \@ARGV, \@leftOverArgs) or die "\n";

$t1 = shift @leftOverArgs or die $usage;
$skull_mask = shift @leftOverArgs or die $usage;
$skelCSF = shift @leftOverArgs or die $usage;
$wmSurfaceLeft = shift @leftOverArgs or die $usage;
$wmSurfaceRight = shift @leftOverArgs or die $usage;
$cls = shift @leftOverArgs or die $usage;
$pve_gm = shift @leftOverArgs or die $usage;
$pve_disc = shift @leftOverArgs or die $usage;
$callosal_mask = shift @leftOverArgs or die $usage;
$output = shift @leftOverArgs or die $usage;

# register the programs
RegisterPrograms(["minccalc", "mincresample", "mincinfo", 
                  "scan_object_to_volume", "surface_mask2", 
                  "laplacian_thickness", "mincdefrag", "dilate_volume", 
                  "mv", "rm", "mincchamfer", "mincblur", "mincmorph", 
                  "make_curvature_volume"]);

if ($Clobber) {
    AddDefaultArgs("minccalc", ["-clobber"]);
}
AddDefaultArgs("minccalc", ["-quiet"]);
AddDefaultArgs("mincblur", ["-quiet"]);
AddDefaultArgs("mincresample", ["-quiet"]);

# create necessary tmp directory
check_output_dirs($TmpDir);

# ===== Main program execution =====
if (! defined($minc_model)){
    $minc_model = $cls;
} else {
    Spawn(["mincresample", "-nearest", "-like", $minc_model, $cls, 
           "${TmpDir}/cls_resampled.mnc"]);
    $cls = "${TmpDir}/cls_resampled.mnc";
}
my $voxel_scale = `mincinfo -attvalue xspace:step $minc_model`;
chomp( $voxel_scale );

# create a binary white matter mask from the white surfaces
$wmMask = "${TmpDir}/wm_mask.mnc";
$wmMaskLeft = "${TmpDir}/wm_mask_left.mnc";
$wmMaskRight = "${TmpDir}/wm_mask_right.mnc";

Spawn(["surface_mask2", "-binary_mask", $minc_model, $wmSurfaceLeft, 
       $wmMaskLeft]);
Spawn(["surface_mask2", "-binary_mask", $minc_model, $wmSurfaceRight, 
       $wmMaskRight]);
Spawn(["minccalc", "-clobber", "-expr", 'out=A[0]||A[1];', 
       $wmMaskLeft, $wmMaskRight, $wmMask] );

unlink( $wmMaskLeft );
unlink( $wmMaskRight );

# resample the CSF skel map to be like the classified map
$rslCSF = "${TmpDir}/csf_rsl.mnc";
Spawn(["mincresample", "-nearest_neighbour", "-like", 
       $minc_model, $skelCSF, $rslCSF]);

# create the grid itself
$expression = 'if(abs(A[3]-1)<0.45){out=10;}else{if(abs(A[3]-2)<0.45){out=5;}else{if(A[2]>0.5){out=0;}else if(A[0]>0.5){out=10;}else if(abs(A[1]-0)<0.5||abs(A[1]-1)<=0.5){out=10;}else if(abs(A[1]-3)<0.5&&A[4]<0.5){out=10;}else{out=5;}}}';

$grid = "${TmpDir}/grid.mnc";
Spawn(["minccalc", "-expression", $expression, $rslCSF, $cls,
       $wmMask, $callosal_mask, $skull_mask, $grid]);

unlink( $wmMask );

# remove dangling voxels: defrag_size should be scaled along one dimension
# only, not 3. Why? Because the defrag is effectively on the CSF skeleton,
# which could be 1-D thin strings of CSF (or 2-D thin sheets of CSF). 20
# is a small threshold for the number of connected CSF voxels that were
# ignored when processing images at 1mm resolution. CL.

my $defrag_size = floor( 20.0 / $voxel_scale );
$defrag_size = 1 if( $defrag_size < 1 );

Spawn(["mincdefrag", $grid, $grid, "5", "6"]);
Spawn(["mincdefrag", $grid, $grid, "10", "27", $defrag_size ]);

# Restore csf skeleton after defrag in regions where k1 curvature is
# negative (inside sulci).

my $t1_blur = "${TmpDir}/t1_4mm";
Spawn([ "mincblur", "-clobber", "-fwhm", 4, $t1, $t1_blur ]);
$t1_blur .= "_blur.mnc";
my $t1_curv = "${TmpDir}/t1_curve_4mm";
Spawn([ "make_curvature_volume", $t1_blur, $t1_curv ]);
$t1_curv .= "_k1.mnc";
unlink( $t1_blur );
my $tmp_grid = "${TmpDir}/tmp_grid.mnc";
$expression = "if((A[1]/$voxel_scale<-1.0)&&(A[0]>0.5)&&(A[2]>0.5)&&(A[3]<0.5)){10}else{A[0]}";
Spawn([ "minccalc", "-clobber", "-expression", $expression,
        $grid, $t1_curv, $rslCSF, $callosal_mask, $tmp_grid ]);
Spawn([ "mincreshape", "-clobber", "-valid_range", 0, 255, 
        "-image_range", 0, 255, $tmp_grid, $grid ]);
Spawn(["rm", "-f", $tmp_grid]);

unlink( $t1_curv );
unlink( "${TmpDir}/t1_curve_4mm" . "_k2.mnc" );
unlink( $rslCSF );

# create the laplacian field

$laplace = "${TmpDir}/laplace.mnc";
Spawn(["laplacian_thickness", "-like", $minc_model, "-potential_only", 
       "-volume-double", "-from_grid", $grid, "none", "none", 
       "-convergence", "1e-8", "-max_iterations", "500", $laplace]);

# Output from laplacian_thickness is saved as x, y, z; so resample in the
# same way as the incoming data (-like $minc_model).
Spawn(["mincresample", "-clobber", "-nearest_neighbour", "-like", $minc_model,
       $laplace, $output]);
unlink( $laplace );

# The next part can be touchy to execute when TmpDir has limited space,
# in particular at a template of 0.5mm since these files are in float.
# So process the in_chamfer and out_chamfer separately, even if it would
# be more efficient to minccalc them together.

# chamfer map in WM area
$in_chamfer="${TmpDir}/in_chamfer.mnc";
Spawn(["minccalc", "-expression", 'if(abs(A[0])<0.5){out=0;}else{out=10;}', 
       $grid, $in_chamfer]);
Spawn(["mincchamfer", "-max_dist", "10", $in_chamfer, "${TmpDir}/chamfer.mnc"]);

$expression = "if(A[0]>=10){out=A[0];}else{out=A[0]-A[1];}";
Spawn(["minccalc", "-clobber", "-expression", $expression, $output,
       "${TmpDir}/chamfer.mnc", $in_chamfer]);
Spawn(["mv", "-f", $in_chamfer, $output]);
unlink( "${TmpDir}/chamfer.mnc" );

# chamfer map in CSF and background area
$out_chamfer="${TmpDir}/out_chamfer.mnc";
Spawn(["minccalc", "-expression", 'if(abs(A[0]-10)<0.5){out=0;}else{out=11;}',
       $grid, $out_chamfer]);
Spawn(["mincchamfer", "-max_dist", "10", $out_chamfer, "${TmpDir}/chamfer.mnc"]);

# combine laplacian field with chamfer maps
Spawn(["minccalc", "-clobber", "-expression", "if(A[0]>=10){A[0]+A[1]}else{A[0]}",
       $output, "${TmpDir}/chamfer.mnc", $out_chamfer]);
unlink( "${TmpDir}/chamfer.mnc" );

# Include partial volume effects at the GM-CSF boundary. Assume that the
# GM-CSF partial volume effects are contained within one connectivity layer
# around the pure GM. All we need is one layer to include the 50% boundary.
# In the expression, the pial border will be at pve_gm=0.50 via the Laplacian
# field=10.

Spawn( ["dilate_volume", $grid, $tmp_grid, 5, 26, 1, $pve_disc, 4.5, 5.5 ]);
Spawn( ["minccalc", "-clobber", "-quiet", "-expression",
     "if(abs(A[1]-5)<0.5&&abs(A[2]-10)<0.5&&A[3]<0.5&&A[4]<0.5){10.0+2.0*(0.5-A[5])}else{A[0]}",
       $out_chamfer, $tmp_grid, $grid, $skelCSF, $callosal_mask, $pve_gm, $output ] );

unlink( $grid );
unlink( $tmp_grid );
unlink( $out_chamfer );

