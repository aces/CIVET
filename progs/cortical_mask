#!/usr/bin/env perl
#

# This step eliminates the skull and meninges by creating a coarse cortical
# surface then removing tissue lying outside of it. The surface produced here
# is a result of a deforming ellipsoid model that shrinks inward in an iterative
# fashion until it finds the pial surface of the cortex. It is not accurate
# enough to become the final pial surface in our corticometric analysis. But
# it does a decent job as a mask to exclude the skull and meninges.

#
# Copyright Alan C. Evans
# Professor of Neurology
# McGill University
#

use strict;
use warnings "all";
use MNI::Startup;
use MNI::FileUtilities qw(check_output_dirs);

# define input variables:

my $cls=$ARGV[0];
my $pve_csf=$ARGV[1];
my $cortex=$ARGV[2];
my $skull_mask=$ARGV[3];
my $brain_mask=$ARGV[4];
my $xfm=$ARGV[5];

# Directory for temporary files.

MNI::FileUtilities::check_output_dirs($TmpDir) or exit 1;

# Apply csf partial volumes to classified image.
my $cls_tmp = "${TmpDir}/cls_tmp.mnc";
my $cls_tmp_masked = "${TmpDir}/cls_tmp_masked.mnc";
&run( "minccalc", "-clobber", "-expression", "if(A[1]<0.10){A[0]}else{1}",
      $cls, $pve_csf, $cls_tmp );

# Apply current full brain mask to classified image (should already be masked).
&run( "mincmath", "-clobber", "-mult", $cls_tmp, $skull_mask, $cls_tmp_masked );
unlink( $cls_tmp );

# run defrag before the cortical surface mask to remove dangling pieces
# of white and grey tissues. This will increase the accuracy of the
# cortical surface by preventing the surface to be attracted to non-
# desired tissues. 

my $cls_defrag = "${TmpDir}/cls_defrag.mnc";
&run( "mincdefrag", $cls_tmp_masked, $cls_defrag, 3, 6 );
&run( "mincdefrag", $cls_defrag, $cls_defrag, 2, 6 );
unlink( $cls_tmp_masked );

# Application of cortical_surface to mask out any remaining non-cortical
# tissue, if any, and the cerebellum. This step can help remove the eyes
# as well.

&run( "cortical_surface", $cls_defrag, $cortex, 1.5, $xfm );

# masking of brain with the cortical surface. No need to remove the boundary
# lines (from scan_object_to_volume) since the mask is very very tight.

&run( "surface_mask2", "-binary_mask", $cls, $cortex, $cls_defrag );
&run( "mincmath", "-clobber", "-mult", $cls_defrag, $skull_mask, $brain_mask );

#Execute a system call.

sub run {
  print "@_\n";
  system(@_)==0 or die "Command @_ failed with status: $?";
}

