#! /usr/bin/env perl
#
# Make an asymmetry map.
#
# Claude Lepage - claude@bic.mni.mcgill.ca
#

use strict;
use warnings "all";
use Getopt::Tabular;
use File::Basename;
use File::Temp qw/ tempdir /;

my($Help, $Usage, $me);
$me = &basename($0);

$Help = <<HELP;
| $me Make an asymmetry map for cortical thickness. Use the resampled
|     thickness maps for a meaningful comparison.
| 
| Problems or comments should be sent to: claude\@bic.mni.mcgill.ca
HELP

$Usage = "Usage: $me [options] left_thickness.txt right_thickness.txt" .
         " diff_thickness.txt [diff_thickness_full.txt]\n".
         "       $me -help to list options\n\n";

my $verbose = 0;
my $clobber = 0;

my @opt_table = (
   ["-verbose", "boolean", 0, \$verbose,
      "be verbose" ],
   ["-clobber", "boolean", 0, \$clobber,
      "clobber existing check files" ],
   );

# Check arguments
&Getopt::Tabular::SetHelp($Help, $Usage);
&GetOptions (\@opt_table, \@ARGV) || exit 1;

die $Usage if( !( $#ARGV == 2 || $#ARGV == 3 ) );

my $left_thickness = shift(@ARGV);
my $right_thickness = shift(@ARGV);
my $diff_thickness = shift(@ARGV);
my $diff_thickness_full = shift(@ARGV);

# check for files
die "$me: Couldn't find input file: $left_thickness\n\n" if (!-e $left_thickness);
die "$me: Couldn't find input file: $right_thickness\n\n" if (!-e $right_thickness);
if(-e $diff_thickness && !$clobber){
   die "$me: $diff_thickness exists, -clobber to overwrite\n\n";
}

# Left/right differences by hemisphere.

&do_cmd( 'vertstats_math', '-sub', $left_thickness, $right_thickness,
         $diff_thickness );

# Left/right differences by hemisphere, on reconstructed full surface
# (left+right surfaces combined).

if( defined $diff_thickness_full ) {

  if(-e $diff_thickness_full && !$clobber){
     die "$me: $diff_thickness_full exists, -clobber to overwrite\n\n";
  }

  # make tmpdir
  my $tmpdir = &tempdir( "$me-XXXXXXXX", TMPDIR => 1, CLEANUP => 1 );

  &do_cmd( 'cat', $left_thickness, $right_thickness, ' > ',
           "${tmpdir}/l_r_thickness.txt" );
  &do_cmd( 'cat', $right_thickness, $left_thickness, ' > ',
           "${tmpdir}/r_l_thickness.txt" );
  &do_cmd( 'vertstats_math', '-sub', "${tmpdir}/l_r_thickness.txt",
           "${tmpdir}/r_l_thickness.txt", $diff_thickness_full );
}

sub do_cmd { 
   print STDOUT "@_\n" if $verbose;
   system("@_") == 0 or die;
}

