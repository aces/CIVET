#!/usr/bin/env perl
#

# Evaluate the surface area of the cortex on the lobes. Areas
# are reported in native space (mm^2).
#
# Copyright Alan C. Evans
# Professor of Neurology
# McGill University
#

use strict;
use warnings "all";

use Getopt::Tabular;
use MNI::Startup;
use MNI::FileUtilities qw(check_output_dirs);

# --- set the help & usage strings ---
my $help = <<HELP;
Compute the lobe areas in native space from the gray surface.
HELP

my $usage = <<USAGE;
Usage: $ProgramName [options] gray.obj surf_labels.txt lobe_areas.dat
       gray.obj - resampled gray surface in stereotaxic space
       surf_labels.txt - surface model labels for definition of lobes
       lobe_areas.dat - output file for areas by lobe
       $ProgramName -help to list options

USAGE

my $transform_xfm = undef;

my @options =
  ( @DefaultArgs,     # from MNI::Startup
    ['-transform', 'string', 1, \$transform_xfm,
     'transformation from native to stereotaxic (if surfaces in stx space)'],
  );

Getopt::Tabular::SetHelp( $help, $usage );

GetOptions( \@options, \@ARGV )
  or exit 1;
die "$usage\n" unless @ARGV == 3;

# define input variables:

my $gray=$ARGV[0];                  # input - resampled gray surface in stx space
my $surface_labels=$ARGV[1];        # input - model surface labels for lobes
my $lobe_areas = $ARGV[2];          # output - areas of the lobes in native space

# Directory for temporary files.

MNI::FileUtilities::check_output_dirs($TmpDir)
or exit 1;


# Transform gray surface from stx to native space to measure area.

my $gray_native = $gray;
if( defined $transform_xfm ) {
  $gray_native = "${TmpDir}/gray_native.obj";
  my $inv_trans = "${TmpDir}/inv_trans.xfm";
  &run( "xfminvert", $transform_xfm, $inv_trans );
  &run( "transform_objects", $gray, $inv_trans, $gray_native );
}

# Define mesh areas at the nodes, applying the lobes on the native
# gray surface. (This should probably be blurred too.)

my $areas = "${TmpDir}/cortex_areas.txt";
&run( "cortex_area", "-surface", $gray_native, "-output", $areas );
&run( "lobe_stats", $areas, $surface_labels, "cortical area", $lobe_areas );
unlink( $areas );


#Execute a system call.

sub run {
  print "@_\n";
  system(@_)==0 or die "Command @_ failed with status: $?";
}

