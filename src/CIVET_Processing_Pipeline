#! /usr/bin/env perl

############################################################################
############################################################################
### Run CIVET processing pipeline within the PMP framework. 
###         
###   In the current version, CIVET requires a quarantine of the software
###   invoked and an environment file.
###                                       
###   Authors:                                  
###         Original pipeline scripts by: Jason Lerch 
###         Pre-modular versions of CIVET: Yasser Ad-Dab'bagh             
###         Modular CIVET: Oliver Lyttelton, J-Sebastian Muehlboeck, 
###                        Yasser Ad-Dab'bagh, Kelvin Mok, Claude Lepage
###         Project started in May 3, 2005       
###       
### For more information, have a look at our documentation at:
###    http://wiki.bic.mni.mcgill.ca/index.php/CIVET
###                                 
############################################################################
############################################################################

use strict;
use FindBin;
use Cwd qw( abs_path );
use Env qw( PATH );

# All modules that will be used in either case are declared here
use Getopt::Tabular;
use PMP::PMP;
use PMP::pbs;
use PMP::sge;
use PMP::spawn;
use PMP::Array;
use MNI::Startup;
use MNI::PathUtilities qw(split_path);
use MNI::FileUtilities qw(check_output_dirs check_output_path);
use MNI::DataDir;

use lib "$FindBin::Bin";
use MRI_Image;
use Processing_Pipeline_Main;

$PATH = "$FindBin::Bin/progs:${PATH}";

# Set interrupt handler (for cleaning of lock files)
$SIG{'INT'} = 'CLEANUP';
$SIG{'TERM'} = 'CLEANUP';

my $version = "1.1.8";
my $versionDate= "June 15, 2007";
my $authors= "Yasser Ad-Dab\'bagh, Oliver Lyttelton, J-Sebastian Muehlboeck, Kelvin Mok, and Claude Lepage";
my $usage = "\nUSAGE:\n$ProgramName -sourcedir <dir> -targetdir <dir> -prefix <prefix> [options] id1 id2 ... idn > <logfile> &\n 
ALTERNATIVE USAGE:\n
$ProgramName -sourcedir <dir> -targetdir <dir> -prefix <prefix> -id-file <idfile> [options]  > <logfile> &\n\n";

my $whatsnew = <<NEW;
    *************************************************************************

    New in $versionDate quarantine, version $version

    1 - simplification of PMP options and -no-granular option.
    2 - specify native voxel sizes in verify image.

    More to come soon:

    0 - improved method to do surface parcellation using non-linear
        surface map.
    1 - susceptibility artefact map now uses brain with cerebellum. *BETA*
    2 - new ICBM templates compatible with non-linear ICBM152 model. *BETA*
    3 - run nu_correct in stereotaxic space on the interpolated 
        original native image, not on the interpolated nu_corrected
        native image. *BETA*
    5 - classification and surface extraction in native space.
    6 - high-resolution surfaces at 320k polygons.
    7 - file compression via PMP to save disk space.

    ********** From previous Mar-30-2007 quarantine, version 1.1.7 **********

    1 - added mean curvature on native mid surfaces.
    2 - added gyrification index.
    3 - cortical thickness now always produced, with tlink method 
        and 20mm blurring kernel as defaults.
    4 - added percentages for tissue types in verify image.
    5 - much improved brain mask, remove -crop-neck option.

    ********** From previous Mar-20-2007 quarantine, version 1.1.6 **********

    1 - support for various interpolation methods from native to
        stereotaxic (linear, cubic, sinc; default is linear).
    2 - error metric for brain mask in native space (in verify image).
    3 - shuffle the PMP steps so that -resetAll actually works.
    4 - file name extension for surface maps is .sm, not .mnc.
        (use rename 's/\.mnc$/\.sm/' */transforms/surfreg/*.mnc).
    5 - can use a 0mm blurring kernel for cortical thickness (no blurring).
    6 - improved cut through callosum to split hemispheres.
    7 - fixed bug in surface registration for infinite loop (on RedHat).
    8 - clear history in minc files to go around likely bug in netCDF 3.6.1.

    ********** From previous Feb-28-2007 quarantine, version 1.1.5 **********

    1 - surface registration and resampling of cortical thickness.
    2 - binary masks in byte format to save disk space.
    3 - specification of data-processing template for voxel size
        (0.50mm, 0.75mm, 1.0mm, 2.0mm, etc; default is 1.0mm).
    4 - check for irregular slice spacing in native images.
    5 - summary of options printed in logs directory of subject.
    6 - validation of input parameters for correct values.
    7 - possibility to easily rerun CIVET with different options
        for cortical thickness method and fwhm.
    8 - keep mid-surfaces in stereotaxic space
    9 - keep surface lobes parcellation at vertices

    ********** From previous Jan-24-2007 quarantine, version 1.1.4 **********

    1 - add surface segmentation based on ANIMAL\'s volume segmentation
        and calculation of cortex area for lobes (eventually will use
        surface registration with surface parcellation).
    2 - iterative correction to the mean and variance of the tissue types
        inside pve as the process converges (option -correct-pve)

    ********** From previous Jan-15-2007 quarantine, version 1.1.3 **********

    1 - add -nothreshold to mritoself for -spectral_mask. This affects only
        the multispectral mask, in very rare cases. If the native mask looks
        wrong and linear registration for no obvious reason, with -spectral_mask,
        then re-run your subject with this fix.
    2 - add -lsq6 to bestlinreg.pl and apply initial transform, if given, to
        mask, only if the mask is given too (does not apply to CIVET).

    ********** From previous Dec-20-2006 quarantine, version 1.1.2 **********

    1 - use the icbm152 mask for nu_correct instead of the avg305 mask,
        since subject is linearly registered to icbm152, not avg305.
    2 - transformation of surfaces to native space, with cortical thickness
        evaluated in native space.
    3 - quarantine based on minc-1.5 (upgraded versions of pcre, pcre++,
        CGAL,BOOST for compilation in 64 bits).
    4 - use icbm152 surface mask for verify image (red lines). Was avg305
        before.
    5 - use classified image from pve instead of classify_clean as input
        to cortical_mask for white matter masking.
    6 - improved mincbet for hyperintense voxels in t1 for masking meninges
    7 - verify.png now shows, in the first row, the brain mask that was 
        used for linear registration (linear registration can live with a
        mask that is slightly off, but not too wrong however)
    8 - make all.q the default queue instead of aces.q
    9 - new flag -lsq12 to CIVET for 12-param linear registration (default
        is still 9-param)
   10 - multispectral classification now produces the good result

    ********** From previous Nov-02-2006 quarantine, version 1.1.0 **********

    The major flaws in the Sep-12-2006 quarantine have been discovered and
    fixed and the former quarantine should no longer be used for any reason.
    The Sep-12-2006 quarantine would give too much white matter, thus bad 
    white surfaces and consequently wrong cortical thickness. All simulations 
    done with the Sep-12-2006 quarantine should be redone using the current 
    one.

    1 - nu_correct must be performed in Talairach space after linear
        registration, where a suitable brain mask is available.
    2 - multispectral brain mask now available in native space (was
        available only in Talairach space before)
    3 - fixed minc interface to mincbet to copy direction cosines of
        axes (was responsible for small rotation of brain in Talairach)
    4 - linear registration is more robust and is now really "best"
    5 - application of the non-linear transformation to the tag points
        used by classify_clean (essential for young children with small
        ventricles or AD subjects with large ventricles)

    There are several other minor enhancements that will not change the
    results but that will make your like easier:

    1 - verify.png image has an outline for the brain mask in Talairach 
        so that you can easily see if the linear registration has been
        successful
    2 - clean-up of the lock files when CIVET is interrupted
    3 - source and target directories may use relative paths
    4 - source files may or may not be gzipped. 

    *************************************************************************
NEW

my $help = <<HELP;

$ProgramName, version $version, released $versionDate.
    Released by $authors.

    Takes any number of multi or single spectral input MINC volumes and
    extracts the cortical surfaces from them utilizing the PMP pipeline
    system. It then calculates cortical thickness at each vertex of the 
    produced cortical surfaces (non-linearly registered) using the t-link 
    metric (in both Talairach and native spaces). It can also produce ANIMAL 
    segmentations, symmetry analyses, regional thickness, surface areas 
    and volumes for brain lobes.

$whatsnew
HELP
Getopt::Tabular::SetHelp($help, $usage);

####################
# Argument handling:
####################

my $reset = undef;
my $command = "printStatus";

# Queuing defaults:
my $PMPtype = "pbs";
my $PMPgranularity = 0;
my $PMPmaxQueued = 10000;
my %PMPconf = ( 'spawn' => { 'queue' => undef,
                             'hosts' => undef,
                             'opts' => undef },
                'sge'   => { 'queue' => "all.q",
                             'hosts' => undef,
                             'opts' => undef },
                'pbs'   => { 'queue' => "brain",
                             'hosts' => undef,
                             'opts' => "-l ncpus=1" } );
my $PMPqueue = $PMPconf{$PMPtype}{queue};
my $PMPhosts = $PMPconf{$PMPtype}{hosts};
my $PMPopts = $PMPconf{$PMPtype}{opts};

my $resetRunning = 1;

############# Default settings for pipeline
my $TemplateSize = "1.00";
my $DefaultRegLinModel = "icbm_avg_152_t1_tal_nlin_symmetric_VI";
my $DefaultRegNLModel = "icbm_avg_152_t1_tal_nlin_symmetric_VI";
my $DefaultRegLinDir = MNI::DataDir::dir("mni_autoreg");
my $DefaultRegNLDir = MNI::DataDir::dir("mni_autoreg");
my $DefaultTemplateDir = MNI::DataDir::dir("ICBM");
my $DefaultTemplateModel = "icbm_template";
my $DefaultSurfRegModelDir = "$FindBin::Bin/models";
my $DefaultSurfRegModel = "surf_reg_model_left.obj";
my $DefaultSurfRegDataTerm = "surf_reg_model_left.txt";

# old settings
#my $DefaultRegLinModel = "icbm_avg_152_t1_tal_lin_symmetric";

############# User defined registration target models 
############# (defaults defined below in the 'registration targets' section)
my $regNLModelDir = undef;
my $regNLModel = undef;
my $regLinModelDir = undef;
my $regLinModel = undef;
my $groupModel = undef;
my $groupModelDir = undef;
my $SurfRegModelDir = undef;
my $SurfRegModel = undef;
my $SurfRegDataTerm = undef;

############# Use model specific pre-registration
my $groupReg = 0;

############# The status report filename:
my $statusReportFile = "CIVET_status_report.csv";

############# Options for how CIVET is to be run: the default will be to run
############# t1 data, cleaning tags, ANIMAL, the production of 82k 
############# polygon surfaces, no symmetry stages, and surface registration. 
my $inputType = "t1only";
my $correctPVE = 0;
my $maskType = "t1only";
my $interpMethod = "trilinear";
my $N3Distance = 200;
my $lsqtype = "-lsq9";
my $VBM = "VBM";
my $animal = "ANIMAL";
my $surface = "SURFACE";
my @thickness = ("tlink","20");

############# Some directories that the user will need to specify
my $sourceDir = undef;
my $target = undef;
my $prefix = undef;
my $sourceSubDir = "noIdSubDir";
my $idTextFile = undef;


############# Options table
my @leftOverArgs;
my @argTbl = (
          ["Execution control", "section"],
          ["-spawn", "const", "spawn", \$PMPtype, "Use the perl system interface to spawn jobs"],
          ["-sge", "const", "sge", \$PMPtype, "Use SGE to spawn jobs."],
          ["-pbs", "const", "pbs", \$PMPtype, "Use PBS to spawn jobs"],
          ["-queue", "string", 1, \$PMPqueue, "Which queue to use"],
          ["-hosts", "string", 1, \$PMPhosts, "Colon separated list of hosts"],
          ["-qopts", "string", 1, \$PMPopts, "Extra options to queuing system"],
          ["-no-granular|-granular", "boolean", 1, \$PMPgranularity, 
           "Granularity level for submission of jobs using queueing system."],
          ["-maxqueued", "string", 1, \$PMPmaxQueued, 
           "Maximum number of jobs that can be submitted at once."],

          ["File options", "section"],
          ["-sourcedir", "string", 1, \$sourceDir, "Directory containing the source files."],
          ["-targetdir", "string", 1, \$target, "Directory where processed data will be placed."],
          ["-prefix", "string", 1, \$prefix, "File prefix to be used in naming output files."],
          ["-id-subdir", "const", "IdSubDir", \$sourceSubDir, "Indicate that the source directory contains sub-directories for each id"],
          ["-id-file", "string", 1, \$idTextFile, "A text file that contains all the subject id\'s (separated by space, tab, return or comma) that CIVET will run on."],

          ["Pipeline options", "section"],
          ["-template", "string", 1, \$TemplateSize, 
           "Define the template for image processing in stereotaxic space (0.50, 0.75, 1.00, 1.50, 2.00, 3.00, 4.00, 6.00)."],
          ["-registration-lin-model", "string", 1, \$regLinModel, 
           "Define the target model for linear registration [default $DefaultRegLinModel]."],
          ["-registration-lin-modeldir", "string", 1, \$regLinModelDir, 
           "Define the directory of the target model for linear registration [default $DefaultRegLinDir]."],
          ["-registration-nl-model", "string", 1, \$regNLModel, 
           "Define the target model for non-linear registration [default $DefaultRegNLModel]."],
          ["-registration-nl-modeldir", "string", 1, \$regNLModelDir, 
           "Define the directory of the target model for non-linear registration [default $DefaultRegNLDir]."],
          ["-surface-model", "string", 1, \$SurfRegModel,
           "Define the surface registration model [default $DefaultSurfRegModel]."],
          ["-surface-dataterm", "string", 1, \$SurfRegDataTerm,
           "Define the surface registration dataterm [default $DefaultSurfRegDataTerm]."],
          ["-surface-modeldir", "string", 1, \$SurfRegModelDir,
           "Define the directory for the surface registration model [default $DefaultSurfRegModelDir]."],

#          ["-group-specific-registration|-normal-registration", "boolean", undef, \$groupReg, "Add a ( pediatric) intermediate model registration."],
#          ["-group-specific-model", "string", 1, \$groupModel, "Define the intermediate registration target for (default pediatric) data."],
#          ["-group-specific-modeldir", "string", 1, \$groupModelDir, "Define the directory of the intermediate registration target for (default pediatric) data."],

          ["CIVET options", "section"],  
          ["-multispectral", "const", "multispectral", \$inputType, 
           "Use T1, T2 and PD native files for tissue classification."],
          ["-correct-pve|-no-correct-pve", "boolean", 1, \$correctPVE, 
           "Apply correction to the mean and variance of tissue types at pve iterations."],
          ["-spectral_mask", "const", "multispectral", \$maskType, 
           "Use T1, T2 and PD native files for brain masking."],
          ["-interp", "string", 1, \$interpMethod,
           "Interpolation method from native to stereotaxic space (\"trilinear\", \"tricubic\", \"sinc\""],
          ["-N3-distance", "string", 1, \$N3Distance, 
           "N3 spline distance in mm (suggested values: 200 for 1.5T scan; 25 for 3T scan)."],
          ["-lsq6", "const", "-lsq6", \$lsqtype,
           "use 6-parameter transformation for linear registration [default -lsq9]" ],
          ["-lsq12", "const", "-lsq12", \$lsqtype,
           "use 12-parameter transformation for linear registration [default -lsq9]" ],
          ["-no-surfaces", "const", "noSURFACE", \$surface, "don\'t build surfaces"],
          ["-no-animal", "const", "noANIMAL", \$animal, "don\'t run volumetric ANIMAL segmentation."],
          ["-thickness", "string", 2, \@thickness,
           "compute cortical thickness and blur [tlink|tlaplace|tnear|tnormal] [kernel size in mm]"],

          ["Pipeline control", "section"],
          ["-run", "const", "run", \$command, "Run the pipeline."],
          ["-status-from-files", "const", "statusFromFiles", \$command, "Compute pipeline status from files"],
          ["-print-stages", "const", "printStages", \$command, "Print the pipeline stages."],
          ["-print-status", "const", "printStatus", \$command, "Print the status of each pipeline."],
          ["-make-graph", "const", "makeGraph", \$command, "Create dot graph file."],
          ["-make-filename-graph", "const", "makeFilenameGraph", \$command, "Create dot graph of filenames."],
          ["-print-status-report", "const", "printStatusReport", \$command, "Writes a CSV status report to file in cwd."],

          ["Stage Control", "section"],
          ["-reset-all", "const", "resetAll", \$reset, "Start the pipeline from the beginning."],
          ["-reset-from", "string", 1, \$reset, "Restart from the specified stage."],
          ["-reset-running|-no-reset-running", "boolean", 1, \$resetRunning, "Restart currently running jobs."],
          );

GetOptions(\@argTbl, \@ARGV, \@leftOverArgs) or die "\n";


############# Basic usage
my @dsids;

if ($idTextFile) {
    open (IDTEXTFILE, "$idTextFile") or die ("Cannot open '$idTextFile': $!");
    # read the whole text file into one string
    my $idstext = "";
    while (my $idline = <IDTEXTFILE>) {
    $idstext .= $idline;
    }
    close (IDTEXTFILE) or die ("Cannot close '$idTextFile': $!");
    # split the string on whitespace (\s) or comma
    @dsids = split(/[\s,]+/, $idstext);
} 
else {
    @dsids = @leftOverArgs or die $usage;
}

unless ($prefix && $target && $sourceDir) {
    die "\n\n*******ERROR********: \n     You must specify -prefix, -targetdir, and -sourcedir \n********************\n\n\n";
}
$target =~ s#/+$##;      # remove trailing / at end of directory name, if any
$target = abs_path( $target );
$sourceDir = abs_path( $sourceDir );

############# Set no file buffering for stdout (buffer is printed every 1 line)
$| = 1;

############# Print the CIVET options list and related error messages
my $DATE = `date`;
chomp( $DATE );
my $UNAME = `uname -s -n -r`;
chomp( $UNAME );
print "\n\n* Pipeline started at $DATE on $UNAME \n";
print "\n* This is CIVET $version, $versionDate \n";
print "\n* CIVET Command line is:\n       $0 @ARGV \n";
print "\n* The source directory is: '$sourceDir' \n";
print "* The target directory is: '$target' \n";
print "* The prefix is: '$prefix' \n";
print "* The PMP class is: '$PMPtype' \n";
print "* The $PMPtype queue type is: '$PMPqueue' \n";
print "* The $PMPtype batch host(s) is/are: '$PMPhosts' \n";
print "* The $PMPtype global options are: '$PMPopts' \n";

############# The -like template (0.50, 0.75, 1.00, 1.50, 2.00, 3.00, 4.00, 6.00)
my $Template = "${DefaultTemplateDir}/${DefaultTemplateModel}_${TemplateSize}mm.mnc";
print "* Template for image-processing is:\n  $Template \n";

############# Used for creating verify_image only.
my $fullpath_surfmask = MNI::DataDir::dir("mni_autoreg")."icbm_avg_152_t1_tal_nlin_symmetric_VI_mask.obj";

############# Define registration targets and directories and print them
unless ($regLinModelDir) {
    $regLinModelDir = $DefaultRegLinDir;
}
unless ($regNLModelDir) {
    $regNLModelDir = $DefaultRegNLDir;
}
print "* Registration linear model directory is:    \n  $regLinModelDir \n";
print "* Registration non-linear model directory is:\n  $regNLModelDir \n";

unless ($regLinModel) {
    $regLinModel = $DefaultRegLinModel;
}
unless ($regNLModel) {
    $regNLModel = $DefaultRegNLModel;
}
my $fullpath_linregmodel = "${regLinModelDir}/${regLinModel}";
my $fullpath_nlregmodel = "${regNLModelDir}/${regNLModel}";

print "* Registration linear model is:    \n   $regLinModel \n";
print "* Registration non-linear model is:\n   $regNLModel \n";

############# Define intermediate registration targets and directories and print them
my $intermediate_model = undef;
if ($groupReg){
    my $tempy =  MNI::DataDir::dir("mni_autoreg");
    $intermediate_model = "${tempy}/nih_chp_avg";
    print "* Group registration model directory is:\n $groupModelDir \n";
}


############# Define Surface registration targets and directories and print them, or die with error message
unless ($SurfRegModelDir) {
    $SurfRegModelDir = $DefaultSurfRegModelDir;
}
unless ($SurfRegModel) {
    $SurfRegModel = $DefaultSurfRegModel;
}
unless ($SurfRegDataTerm) {
    $SurfRegDataTerm = $DefaultSurfRegDataTerm;
}
my $fullpath_surfregmodel = "${SurfRegModelDir}/${SurfRegModel}";
my $fullpath_surfregdataterm = "${SurfRegModelDir}/${SurfRegDataTerm}";

print "* Surface registration model directory is: \n  ${SurfRegModelDir} \n";
print "* Surface registration model is:    \n   ${SurfRegModel} \n";
print "* Surface registration dataterm is: \n   ${SurfRegDataTerm} \n";

############# Print the Pipeline and Stage Control commands

if( !($command eq "run") ) {
	$resetRunning = 0;
}
print "\n\n* Pipeline Control command is: '$command'";
	
if ($reset and $resetRunning ne 0){
    print "\n* Stage control commands are: '$reset' and 'reset-running'";
}
elsif ($reset and $resetRunning eq 0){
    print "\n* Stage control commands are: '$reset' and 'no-reset-running'";
}

print "\n\n\n* Data-set Subject ID(s) is/are: '@dsids'\n\n\n";

my $second_model_dir = "$FindBin::Bin/models";

system("mkdir -p ${target}") if (! -d ${target});


############# An array to store the pipeline definitions for each subject
my $pipes = PMP::Array->new();

#########################################################
# Set up the pipeline output directories for each subject
#########################################################
foreach my $dsid (@dsids) {

    ##### Create image object. #####

    # depending on the two options your files can be in on target
    # or there can be subdirs for every subject
    my $Source_Base;
    if ($sourceSubDir eq "noIdSubDir") {
      $Source_Base = "${sourceDir}/";
    } else {
      $Source_Base = "${sourceDir}/${dsid}/";
    }

    my $image = MRI_Image->new( $Source_Base,
                                $target,
                                $prefix,
                                $dsid,
                                $inputType,
                                $correctPVE,
                                $maskType,
                                $interpMethod,
                                $N3Distance,
                                $lsqtype,
                                $surface,
                                $animal,
                                \@thickness,
                                $fullpath_linregmodel,
                                $fullpath_nlregmodel,
                                $fullpath_surfregmodel,
                                $fullpath_surfregdataterm,
                                $fullpath_surfmask,
                                $Template );

    ##### Create the pipeline environment. #####

    my $pipeline;

    if ($PMPtype eq "spawn") {
        $pipeline = PMP::spawn->new();
    } elsif ($PMPtype eq "sge") {
        $pipeline = PMP::sge->new();
        $pipeline->setQueue($PMPqueue);
        $pipeline->setHosts($PMPhosts) if( defined $PMPhosts );
        $pipeline->setQueueOptions($PMPopts) if( defined $PMPopts );
	$pipeline->setPriorityScheme("later-stages");
    } elsif ($PMPtype eq "pbs") {
        $pipeline = PMP::pbs->new();
        $pipeline->setQueue($PMPqueue);
        $pipeline->setHosts($PMPhosts) if( defined $PMPhosts );
        $pipeline->setQueueOptions($PMPopts) if( defined $PMPopts );
        $pipeline->setPriorityScheme("later-stages");
    }

    # set some generic pipeline options
    $pipeline->name($dsid);
    $pipeline->debug(0);
    $pipeline->statusDir("${target}/${dsid}/logs");
    
##################################### ################# #################
# This used to be the definition of stages... Now it is in modules       
##################################### ################# #################

    CIVET_Main::create_pipeline(
                        \$pipeline,
                        \$image,
                        $fullpath_linregmodel,
                        $fullpath_nlregmodel,
                        $fullpath_surfregmodel,
                        $fullpath_surfregdataterm,
                        $fullpath_surfmask,
                        $intermediate_model, 
                        $Template,
                        $second_model_dir
                        );    

####################################################################################
# Pipeline management
####################################################################################

############# Set the status of the stages.

    if ($reset && ($reset eq "resetAll") ) {
        # Restart all stages
        $pipeline->resetAll();
    } else {
        # restart from a given stage;
        $pipeline->resetFromStage($reset) if( $reset );
        # Rerun any failures from a previous run of this subjects pipe
        $pipeline->resetFailures();
        # Reset running jobs if that's what the user wants.
        $pipeline->resetRunning() if ($resetRunning);
    }

    $pipeline->updateStatus();
 
############# Add this pipe to our happy array of pipes
    
    $pipes->addPipe($pipeline);
    
} ####################################### end of foreach


############# Now run whatever it is that the user wanted done

if ($command eq "printStatus" ) {
    $pipes->printUnfinished();
}

elsif ($command eq "printStatusReport") {
    $pipes->printStatusReport($statusReportFile);
}

elsif ($command eq "statusFromFiles") {
    $pipes->updateFromFiles();
    $pipes->printUnfinished();
}

elsif ($command eq "printStages") {
    $pipes->printStages();
}

elsif ($command eq "makeGraph") {
    $pipes->createDotGraph("dependency-graph.dot");
}

elsif ($command eq "makeFilenameGraph") {
    $pipes->createFilenameDotGraph("filename-dependency-graph.dot","${target}/$dsids[0]/");
}

elsif ($command eq "run") { 
    # register all the programs
    $pipes->registerPrograms();
    $pipes->maxQueued($PMPmaxQueued);
    $pipes->setGranularity($PMPgranularity);
    $pipes->run();
}

else {
    print "huh? Grunkle little gnu, grunkle\n";
}

############# Voila!! #############

# Set interrupt handler (for cleaning of lock files)

sub CLEANUP {
  print "\nCaught signal - cleaning up all lock files\n\n";
  $pipes->cleanup();
  exit(1);
}

